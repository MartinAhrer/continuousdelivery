buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
}

plugins {
    id 'org.ajoberstar.release-opinion' version '1.4.2'
    id "net.ltgt.apt" version "0.6"

    id "org.asciidoctor.convert" version "1.5.2"

    id 'org.unbroken-dome.test-sets' version '1.2.0'
    id 'com.jfrog.bintray' version '1.7'
}

apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

apply plugin: 'idea'

repositories {
    mavenCentral()
}

group 'at.software-craftsmen'

version "${System.getenv('BUILD_NUMBER')?: 'latest'}"

ext {
    spockVersion = '1.1-groovy-2.4-rc-3'
    groovyVersion = '2.4.7'
    lombokVersion = '1.16.8'
    snippetsDir = file('build/generated-snippets')
}

ext['spock.version'] = spockVersion

testSets {
    sharedTest
    localIntegrationTest
    integrationTest
    docsTest { dirName = 'docs'}
}

dependencies {
    apt "org.projectlombok:lombok:$lombokVersion"

    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-hateoas")

    compile("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final")
    compile("org.projectlombok:lombok:$lombokVersion")

    runtime ('org.springframework.data:spring-data-rest-hal-browser')
    runtime ("org.postgresql:postgresql:9.4-1200-jdbc41") {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
    runtime ("com.h2database:h2:1.4.191")

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile("org.springframework:spring-test")
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "org.spockframework:spock-spring:$spockVersion"

    docsTestCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE'
    docsTestCompile 'com.jayway.jsonpath:json-path'
    docsTestCompile 'org.hamcrest:hamcrest-library'

    docsTestCompile sourceSets.sharedTest.output
    docsTestCompile("org.springframework.boot:spring-boot-starter-test")
    docsTestRuntime ("com.h2database:h2:1.4.191")

    integrationTestCompile sourceSets.sharedTest.output
    integrationTestCompile("org.springframework.boot:spring-boot-starter-test")
    integrationTestRuntime ("com.h2database:h2:1.4.191")

    localIntegrationTestCompile sourceSets.sharedTest.output
    localIntegrationTestCompile("org.springframework.boot:spring-boot-starter-test")
    localIntegrationTestRuntime ("com.h2database:h2:1.4.191")
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn docsTest
}

// check: is it still required with ltgt
idea {
    project {
        ipr {
            withXml { provider ->
                // Get XML as groovy.util.Node to work with.
                def projectXml = provider.asNode()

                // Find compiler configuration component.
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }

                // Replace current annotationProcessing
                // that is part of the compiler configuration.
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

build.dependsOn asciidoctor
build.dependsOn localIntegrationTest

bootRepackage {
    classifier = 'exec'
}

import org.apache.tools.ant.filters.ReplaceTokens
ext {
    dockerSrcDir="$projectDir/src/main/docker"
    dockerBuildDir="$buildDir/docker"
}

task buildDockerContext(type: Copy) {
    group = "Docker"
    description = "Prepare for building the image (create the Dockerfile, collect image files, etc.)"

    into dockerBuildDir

    from(dockerSrcDir) {
        filter(ReplaceTokens, tokens: [version: project.version as String])
    }

    with copySpec {
        from bootRepackage
        into "app"
    }
}


publishing {
    publications {
        mavenBoot(MavenPublication) {
            artifact(file("$buildDir/libs/$project.name-$project.version-${bootRepackage.classifier}.jar")) {
                classifier 'exec'
            }
       }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenBoot']

    pkg {
        repo = 'continuousdelivery'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SoftwareCraftsmen/continuousdelivery.git'

        version {
            name = project.version
        }
    }
}