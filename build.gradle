buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.7.2'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.19'
    id 'net.ltgt.apt-idea' version '0.19'
    id 'org.asciidoctor.convert' version '1.5.2'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
    id 'com.jfrog.bintray' version '1.7'

    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'

    id 'com.google.cloud.tools.jib' version '1.2.0'
}

repositories {
    mavenCentral()
}

group = 'at.martinahrer'
project.version = '0.1'
bootJar {
    archiveName = "$baseName.$extension"
}

ext {
    spockVersion = '1.2-groovy-2.4'
    groovyVersion = '2.4.16'
    lombokVersion = '1.18.6'
    logbackVersion = '1.2.3'
    snippetsDir = file('build/generated-snippets')
}

ext['spock.version'] = spockVersion

testSets {
    libraries { sharedTest }
    localIntegrationTest { imports libraries.sharedTest }
    integrationTest { imports libraries.sharedTest }
    docsTest {
        dirName = 'docs'
        imports libraries.sharedTest
    }
}

dependencies {
    apt "org.projectlombok:lombok:$lombokVersion"

    compile("org.springframework.boot:spring-boot-starter")
    runtime("org.springframework.boot:spring-boot-starter-logging")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-hateoas")

    compile("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final")
    compile("org.projectlombok:lombok:$lombokVersion")

    runtime('org.springframework.data:spring-data-rest-hal-browser')
    runtime("org.postgresql:postgresql:9.4-1200-jdbc41") {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }
    runtime("com.h2database:h2:1.4.191")

    runtime("net.logstash.logback:logstash-logback-encoder:4.9") {
        exclude group: "ch.qos.logback", module: "logback-core"
    }
    runtime ("ch.qos.logback:logback-core:${logbackVersion}")
    runtime ("ch.qos.logback:logback-classic:${logbackVersion}")
    runtime("org.slf4j:jcl-over-slf4j:1.7.25")

    sharedTestCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile("org.springframework:spring-test")
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "org.spockframework:spock-spring:$spockVersion"

    docsTestCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE'
    docsTestCompile 'com.jayway.jsonpath:json-path'
    docsTestCompile 'org.hamcrest:hamcrest-library'

    docsTestCompile sourceSets.sharedTest.output
    docsTestCompile("org.springframework.boot:spring-boot-starter-test")
    docsTestRuntime("com.h2database:h2:1.4.191")

    integrationTestCompile sourceSets.sharedTest.output
    integrationTestCompile("org.springframework.boot:spring-boot-starter-test")
    integrationTestRuntime("com.h2database:h2:1.4.191")

    localIntegrationTestCompile sourceSets.sharedTest.output
    localIntegrationTestCompile("org.springframework.boot:spring-boot-starter-test")
    localIntegrationTestCompile('org.springframework.boot:spring-boot')
    localIntegrationTestRuntime("com.h2database:h2:1.4.191")
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'ch.qos.logback' && details.requested.name.startsWith('logback-')) {
            //prefer different version which contains some necessary fixes
            details.useVersion logbackVersion
        }
    }
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn docsTest
}

// check: is it still required with ltgt
//idea {
//    project {
//        ipr {
//            withXml { provider ->
//                // Get XML as groovy.util.Node to work with.
//                def projectXml = provider.asNode()
//
//                // Find compiler configuration component.
//                def compilerConfiguration = projectXml.component.find { component ->
//                    component.'@name' == 'CompilerConfiguration'
//                }
//
//                // Replace current annotationProcessing
//                // that is part of the compiler configuration.
//                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
//                currentAnnotationProcessing.replaceNode {
//                    annotationProcessing {
//                        profile(name: 'Default', default: true, enabled: true) {
//                            processorPath(useClasspath: true)
//                        }
//                    }
//                }
//            }
//        }
//    }
//}

build.dependsOn asciidoctor
build.dependsOn localIntegrationTest

publishing {
    publications {
        bootJava(MavenPublication) {
            artifact bootJar
        }
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

ext {
    dockerSrcDir = "$projectDir/src/main/docker"
    dockerBuildDir = "$buildDir/docker"
}

task buildDockerfile(type: Copy) {
    group = "Docker"
    description = "Prepare for building the image (create the Dockerfile, collect image files, etc.)"

    into dockerBuildDir

    from(dockerSrcDir) {
        filter(ReplaceTokens, tokens: [version: project.version as String])
    }
}

task buildDockerContext(type: Copy, dependsOn: buildDockerfile) {
    group = "Docker"
    description = "Prepare for building the image (create the Dockerfile, collect image files, etc.)"

    into dockerBuildDir

    with copySpec {
        from bootJar
        into "app"
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['bootJava']

    pkg {
        repo = 'continuousdelivery'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/MartinAhrer/continuousdelivery.git'

        version {
            name = project.version
        }
    }
}

task releaseVersion() {
    doLast {
        println project.version
    }
}


jib {
    to {
        image = "martinahrer/continuousdelivery:jib-${project.version}"
    }
}
